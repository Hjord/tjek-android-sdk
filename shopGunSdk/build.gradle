/*******************************************************************************
 * Copyright 2015 ShopGun
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'realm-android'
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    // Custom buildscript closure for SDK module, to allow compilation from other projects
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        // Dependencies checker: run from command line ./gradlew dependencyUpdates -Drevision=release|milestone|integration
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.25.0'
    }
}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

android {

    compileSdkVersion rootProject.compileSdkVersion

    compileOptions {
        // Flag to enable support for the new language APIs (For AGP 4.1+)
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    kapt {
        correctErrorTypes = true
    }

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'sgn_sdk_proguard_rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
}

dependencies {

    def libs = rootProject.libDependencies

    implementation libs.markhor
    implementation libs.zoomlayout
    implementation libs.verso
    implementation libs.materialcolorcreator
    implementation libs.supportv4
    implementation libs.eventbus
    implementation libs.okhttp
    implementation libs.glide
    implementation libs.gson
    implementation libs.appcompat
    implementation libs.appcompatLegacy
    implementation libs.geohash

    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.mockito
    testImplementation libs.okhttpMockwebserver

    // New dependency list
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.3")
    implementation("com.squareup.moshi:moshi:1.13.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.13.0")
    implementation("com.squareup.moshi:moshi-adapters:1.13.0")

}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.shopgun'
                artifactId = 'shopgun-android-sdk'
                version = '5.0.1'
            }
        }
    }
}